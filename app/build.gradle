apply plugin: 'com.android.application'

android {
    // 编译应用的android-SDK的版本
    compileSdkVersion rootProject.ext.compileSdkVersion
    // 构建工具和编译器使用的的版本
    buildToolsVersion "25.0.2"

    defaultConfig {
        //应用标识
        applicationId "com.iwenchaos.gradleguide"
        //最小sdk版本
        minSdkVersion 15
        //编译sdk版本
        targetSdkVersion 25
        //版本号
        versionCode 1
        //版本名称
        versionName "1.0"



        jackOptions{
            enabled true
        }
    }
    //编译类型，在构建编译变种时，可以在此定义
    buildTypes {
        //免费类型
        free{

        }
        //付费类型
        payment{

        }


        debug{
            debuggable true
        }


        release {
            debuggable  false
            //禁止清除无用类型
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        //
        demo{
            debuggable true
            minifyEnabled false
            applicationIdSuffix ".demo"
            versionNameSuffix "-demo"
            buildConfigField "String" ,"API_URL" ,"\"http://demo.com/comsumer-api\""
        }
        //继承已有定义的buildTypes
        subdemo.initWith(buildTypes.demo)
        subdemo{
            //个性化定制内容
            multiDexEnabled true
        }
    }
    //用来创建不同版本
    productFlavors{
        //金卡
        goldcard{
            applicationId 'com.xxx.goldcard'
            versionCode 10101
            versionName '1.1.1'
        }


        blackcard{
            applicationId 'com.xxx.blackcard'
            versionCode 10102
            versionName '1.1.2'
        }
    }

    compileOptions{
        sourceCompatibility org.gradle.api.JavaVersion.VERSION_1_8
        targetCompatibility org.gradle.api.JavaVersion.VERSION_1_8
    }
    // 移除lint检查的error
    lintOptions{
        abortOnError false;
    }
    sourceSets {
        main {
            java{
                srcDir 'src/java'
            }
            resources{
                srcDir 'src/resources'
            }
//            manifest.srcFile 'AndroidManifest.xml'
//            resources.srcDirs = ['src']
//            aidl.srcDirs = ['src']
//            renderscript.srcDirs = ['src']
//            res.srcDirs = ['res']
//            assets.srcDirs = ['src']
        }
        // Move the tests to tests/java, tests/res, etc...    instrumentTest.setRoot('tests')
        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
        androidTest.setRoot('tests')
//        demo.setRoot('')
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    //
    compile 'com.android.support:appcompat-v7:25.1.1'
    //个性化依赖
    demoCompile 'com.android.support:appcompat-v7:25.1.1'
}
